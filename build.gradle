import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'application'
    id 'java'
    id "pmd"
    id "checkstyle"
    id "com.github.spotbugs" version "5.0.8"
}

group = 'net.dirtcraft'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.mikeprimm.com/'
    }
}

dependencies {
    implementation group: 'com.networknt', name: 'json-schema-validator', version: '1.0.76'

    compileOnly 'org.spigotmc:spigot-api:1.7.10-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.7.10-R0.1-SNAPSHOT'

    // repositories that are not being hosted (anymore)
    // adds: ClearLag v3.2.2 (https://dev.bukkit.org/projects/clearlagg)
    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])

    spotbugsPlugins "com.mebigfatguy.sb-contrib:sb-contrib:7.4.7"
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'

    checkstyle 'com.puppycrawl.tools:checkstyle:8.45.1'
}

def targetJavaVersion = 8

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

build {
    dependsOn check
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = "8.8"
    showViolations = true
    maxWarnings(0)
    maxErrors(0)
    configFile file("${projectDir}/config/checkstyle/checkstyle.xml")
    reportsDir file("${buildDir}/reports/checkstyle")
}

tasks.withType(Checkstyle) {
    reports {
        html.enabled true
        xml.enabled false
    }
}

tasks.withType(Pmd) {
    incrementalAnalysis.set(false)
    reports {
        html.enabled true
        xml.enabled true
    }
    ruleSets = ["${projectDir}/config/pmd/pmd.xml"]
}

spotbugs {
    ignoreFailures.set(false)
    effort = "max"
    includeFilter.set(file("${projectDir}/config/spotbugs/include.xml"))
    excludeFilter.set(file("${projectDir}/config/spotbugs/exclude.xml"))
}

tasks.withType(SpotBugsTask) {
    reports {
        html.enabled true
        xml.enabled false
    }
}

jar {
    // keep jar clean
    exclude 'META-INF/**'

    manifest {
        attributes 'Class-Path': configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(' ')
    }

    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) })
}
