plugins {
    id 'application'
    id 'java'
}

group = 'net.dirtcraft'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.mikeprimm.com/'
    }
    maven {
        url = 'https://repo.dmulloy2.net/repository/public/'
    }
    maven {
        url = 'https://repository.mulesoft.org/nexus/content/repositories/public/'
    }
    maven {
        url = 'https://repository.liferay.com/nexus/content/repositories/public/'
    }
}

dependencies {
    implementation group: 'com.networknt', name: 'json-schema-validator', version: '1.0.76'

    compileOnly 'org.spigotmc:spigot-api:1.7.10-R0.1-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.7.10-R0.1-SNAPSHOT'

    // repositories that are not being hosted (anymore)
    // adds: ClearLag v3.2.2 (https://dev.bukkit.org/projects/clearlagg)
    compileOnly fileTree(dir: 'libs', includes: ['*.jar'])
}

def targetJavaVersion = 8

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

jar {
    // keep jar clean
    exclude 'META-INF/**'

    manifest {
        attributes 'Class-Path': configurations.runtimeClasspath.files.collect { "lib/$it.name" }.join(' ')
    }

    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) })
}
